PORTA = $6001
DDRA = $6003
PCR = $600c
IFR = $600d
IER = $600e

kb_wptr = $0000
kb_rptr = $0001
kb_flags = $0002

RELEASE = %00000001
SHIFT   = %00000010

kb_buffer = $0200  ; 256-byte kb buffer 0200-02ff

E  = %01000000
RW = %00100000
RS = %00010000

ACIA_DATA = $5000
ACIA_STATUS = $5001
ACIA_CMD = $5002
ACIA_CTRL = $5003

  .org $8000

reset:
    ; --- ACIA 6551 Initialisation ---
    LDA #$00
    STA ACIA_STATUS
    LDA #$09 
    STA ACIA_CMD
    LDA #$10 
    STA ACIA_CTRL

    ; set VIA to send interrupt on rising edge of CA1 
    lda #$01
    sta PCR
    lda #$82
    sta IER

    ; reset the buffers
    lda #$00
    sta kb_wptr
    sta kb_rptr
    sta kb_flags

loop:
  sei
  lda kb_rptr
  cmp kb_wptr
  cli
  bne key_pressed
  jmp loop

key_pressed:
    ldx kb_rptr
    lda kb_buffer, X
    jsr OSWRCHV

    inc kb_rptr
    jmp loop


OSWRCHV:
    STA ACIA_DATA
WAIT_SETUP: 
    PHX 
    LDX #$12 
WAIT_LOOP:
    DEX
    BNE WAIT_LOOP 
    PLX
OSWRCHV_RETURN:
    RTS


interrupt:
  pha
  txa
  pha

    ;first, check if we are releasing a key
    lda kb_flags
    and #RELEASE
    beq read_key ; move on if we are not releasing a key

    lda kb_flags
    eor #RELEASE
    sta kb_flags
    lda PORTA ; read to clear the interrupt
    jmp exit

read_key:
  lda PORTA
  cmp #$F0
  beq key_release
  tax
  lda keymap,X

push_key:
  ldx kb_wptr
  sta kb_buffer, X
  inc kb_wptr
  jmp exit

key_release:
  lda kb_flags
  ora #RELEASE
  sta kb_flags

exit:
  pla
  tax
  pla
  rti

nmi:
  rti

  .org $fd00
keymap:
  .byte "????????????? `?" ; 00-0F
  .byte "?????q1???zsaw2?" ; 10-1F
  .byte "?cxde43?? vftr5?" ; 20-2F
  .byte "?nbhgy6???mju78?" ; 30-3F
  .byte "?,kio09??./l;p-?" ; 40-4F
  .byte "??'?[=????",$0a,"]?\??" ; 50-5F
  .byte "?????????1?47???" ; 60-6F
  .byte "0.2568",$1b,"??+3-*9??" ; 70-7F
  .byte "????????????????" ; 80-8F
  .byte "????????????????" ; 90-9F
  .byte "????????????????" ; A0-AF
  .byte "????????????????" ; B0-BF
  .byte "????????????????" ; C0-CF
  .byte "????????????????" ; D0-DF
  .byte "????????????????" ; E0-EF
  .byte "????????????????" ; F0-FF
keymap_shifted:
  .byte "????????????? ~?" ; 00-0F
  .byte "?????Q!???ZSAW@?" ; 10-1F
  .byte "?CXDE#$?? VFTR%?" ; 20-2F
  .byte "?NBHGY^???MJU&*?" ; 30-3F
  .byte "?<KIO)(??>?L:P_?" ; 40-4F
  .byte '??"?{+?????}?|??' ; 50-5F
  .byte "?????????1?47???" ; 60-6F
  .byte "0.2568???+3-*9??" ; 70-7F
  .byte "????????????????" ; 80-8F
  .byte "????????????????" ; 90-9F
  .byte "????????????????" ; A0-AF
  .byte "????????????????" ; B0-BF
  .byte "????????????????" ; C0-CF
  .byte "????????????????" ; D0-DF
  .byte "????????????????" ; E0-EF
  .byte "????????????????" ; F0-FF

; Reset/IRQ vectors
  .org $fffa
  .word nmi
  .word reset
  .word interrupt